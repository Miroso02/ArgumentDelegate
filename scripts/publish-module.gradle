apply plugin: 'maven-publish'
apply plugin: 'signing'

ext {
    libraryName = 'argument-delegate'
    publishedGroupId = 'io.github.Miroso02'
    artifact = libraryName.toLowerCase()

    bintrayRepo = 'kotlin'
    bintrayName = libraryName

    libraryDescription = 'Property binding for Android Bundle arguments. Written for simple bundle unpacking for Kotlin users.'
    libraryVersion = '1.0.3'

    siteUrl = "https://github.com/Miroso02/ArgumentDelegate"
    gitUrl = "${siteUrl}.git"

    licenseName = 'BSD 3-Clause License'
    licenseUrl = "$siteUrl/blob/master/LICENSE"
    allLicenses = ["BSD 3-Clause"]

}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = publishedGroupId
version = libraryVersion

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar
//                artifact javadocJar

                pom {
                    name = artifact
                    description = 'Testing maven publish'
                    url = 'https://github.com/Miroso02/ArgumentDelegate'
                    licenses {
                        license {
                            name = licenseName
                            url = licenseUrl
                        }
                    }
                    scm {
                        connection = gitUrl
                        developerConnection = gitUrl
                        url = siteUrl
                    }
                    developers {
                        developer {
                            id = 'zsmb13'
                            name = 'Ivan Stronov'
                            email = 'vaniastronov@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}